plugins {
    id 'org.jetbrains.intellij' version '0.2.17'
    id 'ru.vyarus.animalsniffer' version '1.2.0'
    id 'org.jetbrains.kotlin.jvm' version '1.1.51'
    id 'idea'
}

allprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    group 'com.github.jk1'
    version '0.8'

    repositories {
        mavenCentral()
    }
}

def supportedVersions = ['IC-2017.1', 'IC-2017.2', 'LATEST-EAP-SNAPSHOT', 'LATEST-TRUNK-SNAPSHOT']

intellij {
    version project.hasProperty('ideaVersion') ? project.ideaVersion : 'IC-2017.2'
    plugins 'tasks'  //Bundled plugin dependencies
    updateSinceUntilBuild false
    instrumentCode false
    pluginName 'ytplugin'
    println "This build uses IDEA $version"
}

dependencies {
    compile project(':api')
    compile 'org.nanohttpd:nanohttpd:2.3.0'

    testCompile 'junit:junit:4.12'

    signature 'org.codehaus.mojo.signature:java17:1.0@signature'
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    outputs.upToDateWhen { false }
}

// runs `./gradlew test` several times against all supported IDE versions
task testAllSupportedIdes {
    dependsOn supportedVersions.collect { "runTestsWithIdea$it" }
    supportedVersions.forEach { version ->
        task "runTestsWithIdea$version"(type: GradleBuild) {
            buildFile = 'build.gradle'
            tasks = ['test']
            startParameter.projectProperties = [ideaVersion: version]
        }
    }
}

// Travis CI
check.dependsOn animalsnifferMain, testAllSupportedIdes

