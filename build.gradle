plugins {
    id 'org.jetbrains.intellij' version '0.7.2'
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'idea'
}

allprojects {
    group 'com.github.jk1'
    version '1.2.5'

    repositories {
        mavenCentral()
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// LATEST-TRUNK-SNAPSHOT is getting resolved into an internal JB host for some reason
def supportedVersions = ['IU-2021.1' /*, 'LATEST-TRUNK-SNAPSHOT'*/]

intellij {

    version project.hasProperty('ideaVersion') ? project.ideaVersion : 'IU-2021.1'
    try {
        plugins = ['JavaScriptLanguage', 'JavaScriptDebugger', 'tasks']
    } catch (ignored){
        plugins = ['tasks']
    }

    updateSinceUntilBuild false
    instrumentCode false
    pluginName 'ytplugin'
    println "This build uses IDEA Ultimate $version"

}

idea {
    module {
        downloadSources = true
        outputDir file("$buildDir/idea/main")
        testOutputDir file("$buildDir/idea/test")
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishPlugin {
    token project.findProperty("marketplaceToken") ?: ""
    channels 'stable'
}

dependencies {
    compile project(':api')
    compile 'org.nanohttpd:nanohttpd:2.3.0'
    compile 'org.apache.httpcomponents:httpclient:4.5.13'

    testCompile 'junit:junit:4.12'
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    outputs.upToDateWhen { false }
}

// runs `./gradlew test` several times against all supported IDE versions
task testAllSupportedIdes {
    def taskNames = supportedVersions.collect { "runTestsWithIdea$it" }
    dependsOn taskNames.first()
    supportedVersions.eachWithIndex { version, index ->
        task "runTestsWithIdea$version"(type: GradleBuild) {
            if (index != supportedVersions.size() - 1) {
                dependsOn taskNames.get(index + 1)
            }
            buildName = "runTestsWithIdea$version"
            buildFile = 'build.gradle'
            tasks = ['test']
            startParameter.projectProperties = [ideaVersion: version]
        }
    }
}

// Travis CI
check.dependsOn testAllSupportedIdes

