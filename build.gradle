plugins {
    id 'org.jetbrains.intellij' version '0.4.7'
    id 'org.jetbrains.kotlin.jvm' version '1.2.30'
    id 'idea'
}

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group 'com.github.jk1'
    version '0.9.5'

    repositories {
        mavenCentral()
    }
}

def supportedVersions = ['IC-2018.1', 'IC-2018.2', 'IC-2018.3', 'IC-2019.1', 'LATEST-EAP-SNAPSHOT', 'LATEST-TRUNK-SNAPSHOT']

intellij {
    version project.hasProperty('ideaVersion') ? project.ideaVersion : 'IC-2019.1'
    plugins 'tasks'  //Bundled plugin dependencies
    updateSinceUntilBuild false
    instrumentCode false
    pluginName 'ytplugin'
    println "This build uses IDEA $version"
}

idea {
    module {
        downloadSources = true
        outputDir file("$buildDir/idea/main")
        testOutputDir file("$buildDir/idea/test")
    }
}

dependencies {
    compile project(':api')
    compile 'org.nanohttpd:nanohttpd:2.3.0'

    testCompile 'junit:junit:4.12'
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    outputs.upToDateWhen { false }
}

// runs `./gradlew test` several times against all supported IDE versions
task testAllSupportedIdes {
    dependsOn supportedVersions.collect { "runTestsWithIdea$it" }
    supportedVersions.forEach { version ->
        task "runTestsWithIdea$version"(type: GradleBuild) {
            buildFile = 'build.gradle'
            tasks = ['test']
            startParameter.projectProperties = [ideaVersion: version]
        }
    }
}

// Travis CI
check.dependsOn testAllSupportedIdes

